buildscript {
    ext.kotlinVersion = '1.3.41'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'java-library'
}

group 'com.github.neherim'
version '0.0.2-SNAPSHOT'

repositories {
    gradlePluginPortal()
    mavenCentral()
}

configurations {
    fixtureClasspath
}

tasks.getByName('pluginUnderTestMetadata').getPluginClasspath().from(configurations.fixtureClasspath)

dependencies {
    api 'com.github.spotbugs:com.github.spotbugs.gradle.plugin:2.0.0'
    implementation gradleApi()
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    testImplementation 'junit:junit:4.12'
    fixtureClasspath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
}

gradlePlugin {
    plugins {
        qualityCheckPlugin {
            id = 'com.github.neherim.quality.check'
            implementationClass = 'com.github.neherim.quality.check.QualityCheckPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/neherim/gradle-quality-check-plugin'
    vcsUrl = 'https://github.com/neherim/gradle-quality-check-plugin'

    plugins {
        qualityCheckPlugin {
            displayName = 'Gradle Quality Check Plugin'
            tags = ['check', 'pmd', 'checkstyle', 'spotbugs', 'code quality']
            description = 'Checkstyle for every subproject.'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}